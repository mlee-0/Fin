! Run simulations in a for loop.
! When performing multiplication operations in this script using the * operator, there should not be spaces on either side of *. For example, use `1*2` instead of `1 * 2`.


/CLEAR
/BATCH
! Disable "Solution is done" popup window.
KEYW,pr_sgui,1
! Disable popups.
/UIS,MSGPOP,4
! Set lengths to mm.
/UNITS, USER, LENFACTOR, 1/1000


! Geometric constants.
length = 20
thickness = 1

! Mesh settings.
mesh_divisions_horizontal = 32-1
mesh_divisions_vertical = 8-1

! Material property constants.
thermal_conductivity = 400
specific_heat = 300
mass_density = 10
convection_coefficient = 200
convection_temperature = 22+273.15
temperature_boundary_condition = 100+273.15

! Simulation settings.
initial_temperature = 22+273.15
duration = 100
steps = 10


! Start the for loop.
number_of_data = 1
*DO, i, 1, number_of_data, 1

! Define a table of simulation parameters.
*DIM, parameters, ARRAY, 2, number_of_data
parameters(1,1) = 5, 0.2

height = parameters(1, i, 1)
taper_ratio = parameters(2, i, 1)

/NOPR
KEYW,PR_SET,1
KEYW,PR_STRUC,0
KEYW,PR_THERM,1
KEYW,PR_FLUID,0
KEYW,PR_ELMAG,0
KEYW,MAGNOD,0
KEYW,MAGEDG,0
KEYW,MAGHFE,0
KEYW,MAGELC,0
KEYW,PR_MULTI,0
/GO


! Enter the model creation preprocessor.
/PREP7

! Create the four points of the geometry.
spacing = height*(1-taper_ratio)/2
K, 1, 0, 0, 0
K, 2, length, 0 + spacing, 0
K, 3, length, height - spacing, 0
K, 4, 0, height, 0
! Create the four lines that form the boundary of the geometry.
L, 1, 2
L, 2, 3
L, 3, 4
L, 4, 1
! Create the area using the four lines.
AL, 1, 2, 3, 4

! Define element type.
ET, 1, PLANE55
! Specify plane thickness behavior.
KEYOPT, 1, 3, 3
! Define the thickness.
R, 1, thickness

! Define material properties.
MPTEMP,,,,,,,,
MPTEMP,1,0
MPDATA,KXX,1,,thermal_conductivity
MPTEMP,,,,,,,,
MPTEMP,1,0
MPDATA,C,1,,specific_heat
MPTEMP,,,,,,,,
MPTEMP,1,0
MPDATA,DENS,1,,mass_density

! Define the number of elements along each dimension and create the mesh.
! Horizontal lines have line numbers 1, 3.
LSEL, S, LINE, , 1, 3, 2, 0
LESIZE, ALL, , , mesh_divisions_horizontal, , , , , 1
! Vertical lines have line numbers 2, 4.
LSEL, S, LINE, , 2, 4, 2, 0
LESIZE, ALL, , , mesh_divisions_vertical, , , , , 1

! Create the mesh.
MSHAPE, 0, 2D
MSHKEY, 0
AMESH, 1

! Specify the initial temperature.
ALLSEL, ALL, NODE
IC, ALL, TEMP, initial_temperature
! Specify a reference temperature used for thermal strain calculations.
TREF, initial_temperature
! Specify a temperature boundary condition.
DL, 4, , TEMP, temperature_boundary_condition
! Specify a convection coefficient loading condition.
LSEL, S, , , 1, 3, 1, 0
SFL, ALL, CONV, convection_coefficient, , convection_temperature

! Select all nodes again, which is required for solving.
ALLSEL
FINISH


! Enter the solution processor.
/SOL
! Define a transient analysis.
ANTYPE, TRANS
! Specify stepped loading instead of ramped loading (default).
KBC, 1
! Turn auto-time-stepping off.
AUTOTS, OFF
! Specify the end time of the current load step.
TIME, duration
! Specify the number of substeps. Alternatively, specify the time step using DELTIM.
NSUBST, steps, steps, steps
! Write results for every time step, instead of only the last one (default).
OUTRES, ALL, ALL
! Start the simulation.
SOLVE
FINISH


! Enter the database results postprocessor.
/POST1
! Loop over the data for each time step.
*DO, t, 1, steps, 1
    ! Specify the time step to read.
    SET, 1, t
    ! Get nodal temperatures.
    *VGET, temperature, NODE, , TEMP
    ! Get nodal coordinates.
    *VGET, location_x, NODE, , LOC, X
    *VGET, location_y, NODE, , LOC, Y

    ! Save the results to a file.
    filename = 'fin_%CHRVAL(height)%_%CHRVAL(taper_ratio)%_t=%CHRVAL(t)%'
    *CFOPEN, filename, 'txt'
    *VWRITE, temperature(1), location_x(1), location_y(1)
    (E9.4, ',', F7.4, ',', F7.4)
    *CFCLOS
*ENDDO

/CLEAR

*ENDDO
